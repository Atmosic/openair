# Copyright (c) 2025 Atmosic
#
# SPDX-License-Identifier: Apache-2.0

function(string_contains result main_string substring)
  string(FIND "${main_string}" "${substring}" index)
  if(index EQUAL -1)
    set(${result} FALSE PARENT_SCOPE)
  else()
    set(${result} TRUE PARENT_SCOPE)
  endif()
endfunction()

function(${SYSBUILD_CURRENT_MODULE_NAME}_pre_image_cmake)
  cmake_parse_arguments(PRE_IMAGE_CMAKE "" "IMAGE" "IMAGES" ${ARGN})
  string_contains(is_atm33evk "${BOARD_QUALIFIERS}" "ATM33xx-5")
  set(cur_img ${PRE_IMAGE_CMAKE_IMAGE})
  set(dts_extra_cppflags)
  zephyr_get(SB_ATM_DTS_EXTRA_CPPFLAGS SYSBUILD LOCAL MERGE REVERSE)
  if(SB_ATM_DTS_EXTRA_CPPFLAGS)
    list(APPEND dts_extra_cppflags "${SB_ATM_DTS_EXTRA_CPPFLAGS}")
  endif()
  if(SB_CONFIG_ATMWSTK_CPD200 AND is_atm33evk)
    message(FATAL_ERROR "${BOARD} does not support BLE Stack CPD200")
  elseif(SB_CONFIG_USE_FIXED_ATMWSTK AND NOT is_atm33evk)
    message(FATAL_ERROR "${BOARD} does not support Fixed BLE Stack")
  elseif(SB_CONFIG_USE_FIXED_ATMWSTK OR (SB_CONFIG_ATMWSTK_FULL AND is_atm33evk))
    list(APPEND dts_extra_cppflags "-DATMWSTK=${SB_CONFIG_ATMWSTK_FLAVOR}")
  endif()
  if(NOT "${dts_extra_cppflags}" STREQUAL "")
    set(${cur_img}_DTS_EXTRA_CPPFLAGS "${dts_extra_cppflags}" CACHE STRING "DTS_EXTRA_CPPFLAGS for ${cur_img}")
  endif()
  if("${cur_img}" STREQUAL "mcuboot")
    string_contains(is_atmx2evk "${BOARD}" "ATMEVK-M")
    if(NOT is_atmx2evk)
      set(${cur_img}_DTC_OVERLAY_FILE "${BOARD_DIR}/${BOARD}_mcuboot_bl.overlay" CACHE STRING "DTC_OVERLAY_FILE for ${cur_img}")
    endif()
    return()
  elseif("${cur_img}" STREQUAL "spe")
    return()
  endif()
  set(app_type)
  get_property(app_type TARGET ${cur_img} PROPERTY APP_TYPE)
  if("${app_type}" STREQUAL "MAIN")
    if(SB_CONFIG_ATMWSTK_PD50)
      set_config_bool(${cur_img} CONFIG_ATMWSTK_PD50 "y")
    elseif(SB_CONFIG_ATMWSTK_FULL)
      set_config_bool(${cur_img} CONFIG_ATMWSTK_FULL "y")
    elseif(SB_CONFIG_ATMWSTK_CPD200)
      set_config_bool(${cur_img} CONFIG_ATMWSTK_CPD200 "y")
    endif()
    if(SB_CONFIG_USE_FIXED_ATMWSTK OR (SB_CONFIG_ATMWSTK_FULL AND is_atm33evk))
	set_config_bool(${cur_img} CONFIG_USE_ATMWSTK "y")
    endif()
    if(SB_CONFIG_SPE)
      set(sysbuild_spe_path "${CMAKE_BINARY_DIR}/spe")
      set_property(TARGET ${cur_img} APPEND_STRING PROPERTY CONFIG "CONFIG_SPE_PATH=\"${sysbuild_spe_path}\"\n")
    endif()
    if(SB_CONFIG_BOOTLOADER_MCUBOOT)
      set_property(TARGET ${cur_img} APPEND_STRING PROPERTY CONFIG "CONFIG_MCUBOOT_SIGNATURE_KEY_FILE=\"bootloader/mcuboot/root-ec-p256.pem\"\n")
    endif()
    return()
  endif()
endfunction(${SYSBUILD_CURRENT_MODULE_NAME}_pre_image_cmake)
